;************************************************************************
;*	H8/300H Monitor Program (Normal Mode)		Ver. 2.0A	*
;*		Copyright (C) 2003 Renesas Technology Corp.		*
;************************************************************************
		.PROGRAM  ABORT			; Program Name
		.CPU      300HN			; CPU is H8/300H Normal
		.SECTION  ROM,CODE,ALIGN=2	; ROM Area Section
;************************************************************************
;*	Import Define							*
;************************************************************************
		.IMPORT	OUT_CRLF		; Output CR,LF Module
	 	.IMPORT	OUT_VALUE		; Output Value Module
		.IMPORT	BIN_ASC_4		; 4 Binary to ASCII Module
		.IMPORT	UNDEF_ROU		; Undefine Interrupt Routine
		.IMPORT	STACK			; Stack Area
;************************************************************************
;*	Export Define							*
;************************************************************************
		.EXPORT	INT07			; Abort Handler
;************************************************************************
;*	Abort Handler							*
;*		Input	Nothing						*
;*		Output	Nothing						*
;*		Used Stack Area --> 0(6) Byte				*
;************************************************************************
INT07:		ADDS	#2,ER7			; Increment Stack Pointer
		MOV.L	ER7,@STACK+32		; Move User ER7
		MOV.W	#STACK+32,R7		; Set R7 Save Area Address
		PUSH.L	ER6			; Save User ER6
		PUSH.L	ER5			; Save User ER5
		PUSH.L	ER4			; Save User ER4
		PUSH.L	ER3			; Save User ER3
		PUSH.L	ER2			; Save User ER2
		PUSH.L	ER1			; Save User ER1
		PUSH.L	ER0			; Save User ER0
		MOV.L	@(28,ER7),ER0		; User ER7 --> ER0
		MOV.W	@ER0+,R1		; Move User CCR
		MOV.B	R1H,R1L			;
		MOV.W	@ER0+,E1		; Move User PC
		PUSH.L	ER1			; Save User CCR,PC
		MOV.L	ER0,@(32,ER7)		; Save User ER7
		LDC.B	#H'C0,CCR		; Disable Interrupt
		MOV.W	#ABORT_MSG,R1		; Abort Message --> R1
		BSR	OUT_VALUE:16		; Output Value
		MOV.W	@ER7,R0			; User PC --> R0
		BSR	BIN_ASC_4:16		; Output User PC
		BSR	OUT_CRLF:16		; Output CR,LF
		BRA	UNDEF_ROU:16		; Goto Undefine Routine
ABORT_MSG:	.SDATAZ	"Abort at PC"		; Abort Message
		.END				;
